buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        gradlePluginPortal()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.franzbecker.gradle-lombok' version '5.0.0'
    id 'com.diffplug.spotless' version '6.0.5'
}

apply from: 'utils.gradle'
group 'lyze.dev'
version '0.0.' + getGitHash()
apply from: 'publishing.gradle'

sourceCompatibility = 8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "space.earlygrey:shapedrawer:$shapeDrawerVersion"

    implementation "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "org.mockito:mockito-core:$mockitoVersion"

    testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"

    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

spotless {
    lineEndings 'UNIX'

    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'

        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }

    java {
        importOrder()
        removeUnusedImports()

        indentWithSpaces(4)

        eclipse().configFile('eclipse-formatter.xml')
    }

    groovyGradle {
        target '*.gradle'
        greclipse().configFile('eclipse-formatter.xml')
    }
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = "full"
        showStackTraces = true
        showExceptions = true
        showCauses = true
        displayGranularity = 0
        events = [
            "standardOut",
            "standardError",
            "started",
            "passed",
            "skipped",
            "failed"
        ]
        showStandardStreams = true
    }

    reports {
        junitXml.configure {
            enabled(true)
        }
    }

    if (project.hasProperty("lwjgl")) {
        useJUnitPlatform {
            includeTags "lwjgl"
        }
    } else {
        useJUnitPlatform {
            excludeTags 'lwjgl'
        }
    }
}

import io.franzbecker.gradle.lombok.task.DelombokTask

task delombok(type: DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = false
}

compileJava {
    options.incremental = true
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
